@tool
class_name LevelTransition extends Area2D

enum SIDE {LEFT, RIGHT, TOP, BOTTOM}

@export_file( "*.tscn" ) var level
@export var target_transition_area : String = "LevelTransition"

@export_category("Collision Area Settings")

@export_range(1, 12, 1, "or_greater") var size: int = 2 :
	set( _v ):
		size = _v
		_update_area()

@export var side: SIDE = SIDE.LEFT :
	set( _v ):
		side = _v
		_update_area()

@export var snap_to_grid: bool = false :
	set (_v):
		_snap_to_grid()

@onready var collision_shape: CollisionShape2D = $CollisionShape2D

func _ready() -> void:
	_update_area()
	if Engine.is_editor_hint():
		return
	
	#monitoring = false
	
	
	body_entered.connect(_player_entered)
	
	pass


func _player_entered(_p: Node2D) -> void:
	if LevelManager.transition_locked:
		return
		
	LevelManager.load_new_level(level, target_transition_area, Vector2.ZERO)
	pass


func _update_area() -> void:
	var new_rect: Vector2 = Vector2(170, 170)
	var new_posotion: Vector2 = Vector2.ZERO
	
	if side == SIDE.TOP:
		new_rect.x *= size
		new_posotion.y -= 85
		
	elif side == SIDE.BOTTOM:
		new_rect.x *= size
		new_posotion.y += 85
		
	elif side == SIDE.LEFT:
		new_rect.y *= size
		new_posotion.x -= 85
		
	elif side == SIDE.RIGHT:
		new_rect.y *= size
		new_posotion.x += 85
	
	if collision_shape == null:
		collision_shape = get_node("CollisionShape2D")
	
	collision_shape.shape.size = new_rect
	collision_shape.position = new_posotion


func _snap_to_grid() -> void:
	position.x = round (position.x / 85 ) * 85
	position.y = round (position.y / 85 ) * 85
